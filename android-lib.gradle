apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
group = project.APP_GROUP

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
}

android {
    compileSdkVersion Integer.parseInt(project.TARGET_SDK)
    buildToolsVersion project.BUILD_TOOLS

    def kauMinSdk = project.MIN_SDK
    if (project.hasProperty('kauSubModuleMinSdk'))
        kauMinSdk = kauSubModuleMinSdk

    def kauResourcePrefix = "kau_"
    if (project.hasProperty('kauSubModuleResourcePrefix'))
        kauResourcePrefix = kauSubModuleResourcePrefix

    resourcePrefix kauResourcePrefix

    defaultConfig {
        minSdkVersion Integer.parseInt(kauMinSdk)
        targetSdkVersion Integer.parseInt(project.TARGET_SDK)
        consumerProguardFiles 'progress-proguard.txt'
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warningsAsErrors true
        disable 'LogNotTimber',
                'UnusedResources',
                'ContentDescription',
                'RtlSymmetry',
                'RtlHardcoded',
                'RtlEnabled',
                'Overdraw'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.res.srcDirs += 'src/main/res-public'
    }

    testOptions.unitTests {
        // Don't throw runtime exceptions for android calls that are not mocked
        returnDefaultValues = true

        // Always show the result of every unit test, even if it passes.
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }
}

dependencies {
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:${KOTLIN}"
}