buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$GRADLE_PLUGIN"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$MAVEN_PLUGIN"
        classpath "com.github.triplet.gradle:play-publisher:$PLAY_PUBLISH_PLUGIN"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$DEX_COUNT_PLUGIN"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task generateChangelogMd {
    def parsedProjectXml = (new XmlParser()).parse("$project.rootDir/sample/src/main/res/xml/kau_changelog.xml")
    def sw = new StringWriter()
    sw.append("# Changelog\n")
    parsedProjectXml.depthFirst().each {
        switch (it.name()) {
            case "version":
                sw.append("\n## ${it.@title}\n")
                break
            case "item":
                if (it.@text?.trim())
                    sw.append("* ${it.@text}\n")
        }
    }
    def changelogMd = new File("$project.rootDir/docs/Changelog.md")
    changelogMd.write(sw.toString())
}

subprojects {

    repositories {
        jcenter()
    }

    if (it.name == 'gradle-plugin')
        return

    repositories {
        google()
        maven { url "https://jitpack.io" }
    }
}