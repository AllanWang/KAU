// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${KOTLIN}"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def portions = project.VERSION_NAME.tokenize('.')
int code = 0
for (i in 1..4) {
    if (portions.size() >= i && portions[i - 1].isInteger())
        code += portions[i - 1].toInteger()
    code *= 100
}
code /= 100
println "KAU Project Version Name: " + project.VERSION_NAME
println "KAU Project Version Code: " + code
project.ext.VERSION_CODE = code

task clean(type: Delete) {
    delete rootProject.buildDir
}

task generateChangelogMd {
    def parsedProjectXml = (new XmlParser()).parse("$project.rootDir/sample/src/main/res/xml/kau_changelog.xml")
    def sw = new StringWriter()
    sw.append("# Changelog\n")
    parsedProjectXml.depthFirst().each {
        switch (it.name()) {
            case "version":
                sw.append("\n## ${it.@title}\n")
                break
            case "item":
                if (it.@text?.trim())
                    sw.append("* ${it.@text}\n")
        }
    }
    def changelogMd = new File("$project.rootDir/docs/Changelog.md")
    changelogMd.write(sw.toString())
}

subprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}